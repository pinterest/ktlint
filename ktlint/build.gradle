plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'com.vanniktech.maven.publish'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

jar {
  manifest {
    attributes 'Main-Class': 'com.pinterest.ktlint.Main'
  }
}

shadowJar {
  mergeServiceFiles()
}

artifacts {
  archives shadowJar
}

dependencies {
  implementation project(':ktlint-core')
  implementation project(':ktlint-reporter-checkstyle')
  implementation project(':ktlint-reporter-json')
  implementation project(':ktlint-reporter-plain')
  implementation project(':ktlint-ruleset-experimental')
  implementation project(':ktlint-ruleset-standard')
  implementation project(':ktlint-test')
  implementation deps.kotlin.stdlib
  implementation deps.kotlin.compiler
  implementation deps.klob
  implementation deps.aether.api
  implementation deps.aether.spi
  implementation deps.aether.util
  implementation deps.aether.impl
  implementation deps.aether.connector.basic
  implementation deps.aether.transport.file
  implementation deps.aether.transport.http
  implementation deps.guava
  implementation deps.slf4j
  implementation deps.aetherProvider
  implementation deps.picocli

  testImplementation deps.junit
  testImplementation deps.assertj
}

// Implements https://github.com/brianm/really-executable-jars-maven-plugin maven plugin behaviour.
// To check details how it works, see http://skife.org/java/unix/2011/06/20/really_executable_jars.html.
tasks.register("shadowJarExecutable", DefaultTask.class) {
  description = "Creates self-executable file, that runs generated shadow jar"
  group = "Distribution"

  inputs.files tasks.named("shadowJar")
  outputs.file "$buildDir/run/ktlint"

  doLast {
    File execFile = outputs.files.singleFile
    execFile.withOutputStream {
      it.write "#!/bin/sh\n\nexec java -Xmx512m -jar \"\$0\" \"\$@\"\n\n".bytes
      it.write inputs.files.singleFile.bytes
    }
    execFile.setExecutable(true, false)
  }
}
