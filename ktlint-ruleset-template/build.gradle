plugins {
  id 'ktlint-kotlin-common' // replace it with 'org.jetbrains.kotlin.jvm'
  id 'java-library'
  id 'maven-publish'
}

group = 'com.github.username'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

configurations {
  ktlint
}

dependencies {
  ktlint projects.ktlint

  compileOnly projects.ktlintCore

  testImplementation projects.ktlintCore
  testImplementation projects.ktlintTest
  testImplementation libs.junit5
  testImplementation libs.assertj
}

test {
  useJUnitPlatform()
}

task ktlint(type: JavaExec, dependsOn: classes, group: LifecycleBasePlugin.VERIFICATION_GROUP) {
  mainClass.set("com.pinterest.ktlint.Main")
  // adding compiled classes to the classpath so that ktlint would validate project's sources
  // using its own ruleset (in other words to dogfood)
  classpath = configurations.ktlint + sourceSets.main.output
  args '--debug', 'src/**/*.kt'
}

check.dependsOn ktlint

afterEvaluate {
  publishing {
    publications {
      mavenJava(MavenPublication) {
        pom {
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
        }
      }
    }
  }
}
