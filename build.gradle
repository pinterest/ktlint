plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.70' apply false
  id 'com.vanniktech.maven.publish' version '0.8.0' apply false
}

ext.versions = [
  'kotlin': '1.3.70',
  'gradle': '5.6.2'
]

ext.deps = [
  'kotlin'        : [
    'stdlib'  : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    'compiler': "org.jetbrains.kotlin:kotlin-compiler-embeddable:${versions.kotlin}"
  ],
  'klob'          : 'com.github.shyiko.klob:klob:0.2.1',
  ec4j            : 'org.ec4j.core:ec4j-core:0.2.0',
  'picocli'       : 'info.picocli:picocli:3.9.6',
  // Testing libraries
  'junit'         : 'junit:junit:4.12',
  'assertj'       : 'org.assertj:assertj-core:3.12.2',
  'jimfs'         : 'com.google.jimfs:jimfs:1.1'
]

configurations {
  ktlint
}

dependencies {
  ktlint project(':ktlint')
}

task ktlint(type: JavaExec, group: LifecycleBasePlugin.VERIFICATION_GROUP) {
  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = 'com.pinterest.ktlint.Main'
  args '*/src/**/*.kt'
}

allprojects {
  repositories {
    gradlePluginPortal()
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
      jvmTarget = '1.8'
    }
  }
}

/**
 * Configures "wrapper" task to use specific Gradle version and distribution type.
 */
tasks.withType(Wrapper.class).configureEach {
  gradleVersion = versions.gradle
  distributionType = Wrapper.DistributionType.BIN
}

// Deployment tasks
String getGithubToken() {
  if (project.hasProperty("servers.github.privKey")) {
    return project.'servers.github.privKey'
  } else {
    logger.warn("No github token specified")
    return ""
  }
}

// Put "servers.github.privKey" in "$HOME/.gradle/gradle.properties".
def announceTask = tasks.register("announceRelease", Exec.class) { announceTask ->
  group = "Help"
  description = "Runs .announce script"
  subprojects { subproject ->
    subproject.tasks.named("uploadArchives") { task ->
      announceTask.dependsOn ":${subproject.name}:${task.name}"
    }
  }

  commandLine './.announce', '-y'
  environment VERSION: "${project.'VERSION_NAME'}"
  environment GITHUB_TOKEN: "${getGithubToken()}"
}

tasks.register("publishNewRelease", DefaultTask.class) {
  group = "Help"
  description = "Triggers uploading new archives and publish announcements"
  dependsOn announceTask
}
