pluginManagement {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    if (settings.hasProperty('kotlinDev')) {
      maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
      maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
  }

  plugins {
    def kotlinVersion = settings.hasProperty("kotlinDev") ? "1.4.10-rc-345" : "1.4.0"
    id 'org.jetbrains.kotlin.jvm' version kotlinVersion
    id 'com.github.breadmoirai.github-release' version '2.2.12'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.gradle.crypto.checksum' version '1.1.0'
  }
}

// Pass '-PkotlinDev' to command line to enable kotlin-in-development version
gradle.ext.isKotlinDev = settings.hasProperty("kotlinDev")
if (gradle.ext.isKotlinDev) {
  logger.warn("Enabling kotlin dev version")
}

rootProject.name = 'ktlint'

include ':ktlint'
include ':ktlint-core'
include ':ktlint-reporter-checkstyle'
include ':ktlint-reporter-json'
include ':ktlint-reporter-html'
include ':ktlint-reporter-plain'
include ':ktlint-ruleset-experimental'
include ':ktlint-ruleset-standard'
include ':ktlint-ruleset-template'
include ':ktlint-test'
